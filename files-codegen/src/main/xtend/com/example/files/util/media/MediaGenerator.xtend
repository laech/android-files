package com.example.files.util.media

import java.io.File
import java.net.URL
import java.util.Map
import java.util.TreeMap
import org.jdom2.input.SAXBuilder
import java.util.Set

import static com.google.common.base.Charsets.*
import static com.google.common.io.Files.*
import static java.lang.System.*
import static java.util.Locale.*

class MediaGenerator {

  static val MEDIA_URL =
    new URL("http://svn.apache.org/repos/asf/tomcat/trunk/conf/web.xml")

  def static void main(String[] args) {
    val in = MEDIA_URL.openStream
    try {

      val root = new SAXBuilder().build(in).rootElement
      val ns = root.namespace
      val medias = new TreeMap<String, String>
      val mimes = root.getChildren("mime-mapping", ns)
      mimes.forEach[mime |
        medias.put(
          mime.getChildText("extension", ns).toLowerCase(ENGLISH),
          mime.getChildText("mime-type", ns).toLowerCase(ENGLISH)
        )
      ]

      write(generateMedias(medias), output("Medias.java"), UTF_8)
      write(generateImages(medias), output("Images.java"), UTF_8)

    } finally {
      in.close
    }
  }

  def static output(String name) {
    new File(getProperty("user.home") + "/Desktop/" + name)
  }

  def static generateMedias(Map<String, String> types) '''
    // This file is generated by «typeof(MediaGenerator).name»

    package com.example.files.media;

    import static java.util.Collections.unmodifiableMap;
    import static java.util.Locale.ENGLISH;

    import java.util.HashMap;
    import java.util.Map;

    class Medias {

      private static final Map<String, String> TYPES = mediaTypes();

      private static Map<String, String> mediaTypes() {
        «map(types)»
      }

      /**
       * Gets the media type for the given file extension.
       *
       * @param extension the file extension without the ".", case insensitive
       */
      public static String get(String extension) {
        return TYPES.get(extension.toLowerCase(ENGLISH));
      }

    }
  '''

  def static map(Map<String, String> map) '''
    Map<String, String> map = new HashMap<String, String>(«capacity(map.size)»);
    «FOR entry : map.entrySet»
    map.put("«entry.key»", "«entry.value»");
    «ENDFOR»
    return unmodifiableMap(map);
  '''

  def static generateImages(Map<String, String> types) {
    val texts = types.filter(key, value | value.startsWith("text")).keySet
    val images = types.filter(key, value | value.startsWith("image")).keySet
    val audios = types.filter(key, value | value.startsWith("audio")).keySet
    val videos = types.filter(key, value | value.startsWith("video")).keySet
    '''
    // This file is generated by «typeof(MediaGenerator).name»

    package com.example.files.media;

    import static java.util.Collections.unmodifiableSet;
    import static java.util.Locale.ENGLISH;

    import java.util.HashSet;
    import java.util.Set;
    import com.example.files.R;

    class Images {

      private static final Set<String> AUDIOS = audios();
      private static final Set<String> VIDEOS = videos();
      private static final Set<String> IMAGES = images();
      private static final Set<String> TEXTS = texts();

      private static Set<String> audios() {
        «set(audios)»
      }

      private static Set<String> videos() {
        «set(videos)»
      }

      private static Set<String> images() {
        «set(images)»
      }

      private static Set<String> texts() {
        «set(texts)»
      }

      public static int get(String extension) {
        String ext = extension.toLowerCase(ENGLISH);
        if (ext.equals("pdf")) return R.drawable.ic_file_pdf;
        if (AUDIOS.contains(ext)) return R.drawable.ic_image;
        if (VIDEOS.contains(ext)) return R.drawable.ic_image;
        if (IMAGES.contains(ext)) return R.drawable.ic_image;
        if (TEXTS.contains(ext)) return R.drawable.ic_image;
        if (AUDIOS.contains(ext)) return R.drawable.ic_image;
        return R.drawable.ic_file;
      }
    }
    '''
  }

  def static set(Set<String> set) '''
    Set<String> set = new HashSet<String>(«capacity(set.size)»);
    «FOR element : set»
    set.add("«element»");
    «ENDFOR»
    return unmodifiableSet(set);
  '''

  def static capacity(int expectedSize) {
    return expectedSize + expectedSize / 3;
  }
}
