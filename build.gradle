buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'
    }
}

subprojects {
    repositories {
        mavenCentral()
    }

    def androidConfig = {
        android {
            compileSdkVersion 19
            buildToolsVersion "19.1"

            defaultConfig {
                minSdkVersion 14
                targetSdkVersion 19
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_7
                targetCompatibility JavaVersion.VERSION_1_7
            }

            lintOptions {
                abortOnError false
            }

            // Remove files from third party jars that causes 'duplicate files' build issues
            packagingOptions {
                exclude 'META-INF/services/javax.annotation.processing.Processor'
                exclude 'META-INF/LICENSE.txt'
                exclude 'META-INF/NOTICE.txt'
                exclude 'LICENSE.txt'
            }
        }
    }

    def androidApp = com.android.build.gradle.AppPlugin
    def androidLib = com.android.build.gradle.LibraryPlugin

    if (plugins.hasPlugin('android') || plugins.hasPlugin('android-library')) {
        apply plugin: 'android-apt'
    }

    plugins.withType(androidLib, androidConfig)
    plugins.withType(androidApp, androidConfig)
}

ext.libs = [:]

libs.auto_value = [
        'javax.annotation:javax.annotation-api:1.2',
        'com.google.auto.value:auto-value:1.0-rc1'
]
libs.android_support = [
        'com.android.support:support-v4:19.0.+',
        'com.android.support:support-v13:19.0.+'
]
libs.tika = 'org.apache.tika:tika-core:1.4'
libs.otto = 'com.squareup:otto:1.3.+'
libs.guava = 'com.google.guava:guava:17.+'
libs.picasso = 'com.squareup.picasso:picasso:2.1.+'
libs.joda_time = 'joda-time:joda-time:2.2'
libs.commons_io = 'commons-io:commons-io:2.4'
libs.commons_lang = 'org.apache.commons:commons-lang3:3.1'
libs.google_play_services = 'com.google.android.gms:play-services:4.0.+'

libs.mockito = [
        'org.mockito:mockito-core:1.9.+',
        'com.google.dexmaker:dexmaker:1.0',
        'com.google.dexmaker:dexmaker-mockito:1.0'
]
libs.hamcrest = 'org.hamcrest:hamcrest-library:1.3'
